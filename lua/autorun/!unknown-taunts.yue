PLAYER, ENTITY = FindMetaTable( "Player" ), FindMetaTable( "Entity" )
:sub, :lower, :find, :match, :Trim = string
tonumber = tonumber
isnumber = isnumber
isvector = isvector
isangle = isangle
istable = istable
CurTime = CurTime

addonName = "Unknown Taunts"

lib = uTaunt
unless istable( lib )
    lib = { Name: addonName }
    global uTaunt = lib

coopDances = list.GetForEdit( "uTaunt - Coop", false )

timer.Simple 0.5, ->
    for sequenceName, value in pairs( coopDances )
        if not isnumber( value ) or value < 1
            continue

        sequenceName, danceID = match( sequenceName, "^([%a_]+_?)(%d+)$" )
        if sequenceName == nil or danceID == nil
            continue

        danceID = tonumber( danceID )
        if danceID == nil
            continue

        for index = 1, value
            coopDances[ sequenceName .. ( danceID + index ) ] = 0

:GetNW2Var, :SetNW2Var, :LookupSequence, :SequenceDuration, :IsValid = ENTITY
:Add, :Run = hook
:Alive = PLAYER

ACT_GMOD_TAUNT_DANCE = ACT_GMOD_TAUNT_DANCE
GESTURE_SLOT_CUSTOM = GESTURE_SLOT_CUSTOM

isCPlayingTaunt = PLAYER.IsCPlayingTaunt
unless isCPlayingTaunt
    isCPlayingTaunt = PLAYER.IsPlayingTaunt
    PLAYER.IsCPlayingTaunt = isCPlayingTaunt

isPlayingTaunt = ( ply ) ->
    return GetNW2Var( ply, "uTaunt-name" ) ~= nil

lib.IsPlayingTaunt = isPlayingTaunt

getStartTime = ( ply ) ->
    return GetNW2Var( ply, "uTaunt-start", 0 )

lib.GetStartTime = getStartTime

PLAYER.IsPlayingTaunt = ( ply ) ->
    return isCPlayingTaunt( ply ) or isPlayingTaunt( ply )

getCycle = nil
do

    :Clamp = math

    getCycle = ( ply, sequenceID, startTime ) ->
        return Clamp( ( CurTime! - startTime ) / SequenceDuration( ply, sequenceID ), 0, 1 )

    lib.GetCycle = getCycle

do

    length, id, duration = 0, 0, 0
    :GetSequenceList = ENTITY

    lib.FindSequences = ( entity, pattern ) ->
        sequences, length = {}, 0

        for name in *GetSequenceList( entity )
            if find( name, pattern, 1, false ) == nil
                continue

            id = LookupSequence( entity, name )
            if id < 1
                continue

            duration = SequenceDuration( entity, id )
            if duration <= 0
                continue

            length += 1
            sequences[ length ] = { :id, :name, :duration }

        return sequences, length

isValidTauntingPlayer = ( ply ) ->
    return ply and IsValid( ply ) and Alive( ply ) and isPlayingTaunt( ply )

lib.IsValidTauntingPlayer = isValidTauntingPlayer

if SERVER

    utaunt_coop_distance = CreateConVar( "utaunt_coop_distance", "256", FCVAR_ARCHIVE, "Minimum required distance to join in a co-op taunt.", 0, 16384 )
    utaunt_allow_weapon = CreateConVar( "utaunt_allow_weapon", "0", FCVAR_ARCHIVE, "Allow players to hold weapons in their hands while taunting.", 0, 1 )
    utaunt_collisions = CreateConVar( "utaunt_collisions", "0", FCVAR_ARCHIVE, "Allow players to collide with each other while taunting.", 0, 1 )
    resource.AddWorkshop( "3161527342" )
    :SetCollisionGroup = ENTITY

    Add "PlayerInitialSpawn", addonName, ( ply ) ->
        ply.m_tUnknownTauntPlayers = {}

    do

        isbool = isbool

        lib.Stop = ( ply ) ->
            sequenceName = GetNW2Var( ply, "uTaunt-name" )
            if sequenceName == nil
                return false

            ply\AnimResetGestureSlot( GESTURE_SLOT_CUSTOM )
            SetNW2Var( ply, "uTaunt-angles", nil )
            SetNW2Var( ply, "uTaunt-name", nil )

            -- Coop players
            players = ply.m_tUnknownTauntPlayers
            for index = 1, #players
                otherPlayer = players[ index ]
                players[ index ] = nil

                if isValidTauntingPlayer( otherPlayer )
                    lib.Stop( otherPlayer )

            -- Collision group
            collisionGroup = ply.m_iUnknownTauntCollisionGroup
            if isnumber( collisionGroup )
                SetCollisionGroup( ply, collisionGroup )

            ply.m_iUnknownTauntCollisionGroup = nil

            -- Player pushing
            avoidPlayers = ply.m_bUnknownTauntAvoidPlayers
            if isbool( avoidPlayers )
                ply\SetAvoidPlayers( avoidPlayers )

            ply.m_bUnknownTauntAvoidPlayers = nil

            -- Weapon
            className = ply.m_sUnknownTauntWeapon
            if isstring( className )
                ply\SelectWeapon( className )

            ply.m_sUnknownTauntWeapon = nil

            -- Origin
            origin = ply.m_vUnknownTauntOrigin
            if isvector( origin )
                ply\SetPos( origin )

            ply.m_vUnknownTauntOrigin = nil

            -- Angles
            angles = ply.m_aUnknownTauntAngles
            if isangle( angles )
                ply\SetEyeAngles( angles )
                ply\SetAngles( angles )

            ply.m_aUnknownTauntAngles = nil

            -- Sound/Music
            cSound = ply.m_csUnknownTauntSound
            if cSound and cSound\IsPlaying!
                cSound\FadeOut( 1 )

            ply.m_csUnknownTauntSound = nil

            Run( "PlayerStoppedUnknownTaunt", ply, sequenceName )
            return true

    Add "PlayerDisconnected", addonName, lib.Stop
    Add "PostPlayerDeath", addonName, lib.Stop

    do

        CreateSound = CreateSound
        :FindInSphere = ents
        :Exists = file

        lib.Start = ( ply, sequenceName, force, cycle, startOrigin, startAngles ) ->
            sequenceID = LookupSequence( ply, sequenceName )
            if sequenceID < 1
                return false

            unless force
                if Run( "PlayerShouldUnknownTaunt", ply, sequenceID ) ~= true or Run( "PlayerShouldTaunt", ply, ACT_GMOD_TAUNT_DANCE ) == false
                    return false

                if isnumber( coopDances[ sequenceName ] ) and coopDances[ sequenceName ] > 0 and utaunt_coop_distance\GetInt! > 0
                    for otherPlayer in *FindInSphere( ply\GetPos!, utaunt_coop_distance\GetInt! )
                        if otherPlayer\IsPlayer! and Alive( otherPlayer ) and otherPlayer ~= ply and GetNW2Var( otherPlayer, "uTaunt-name" ) == sequenceName and lib.Join( ply, otherPlayer )
                            return true

            duration = SequenceDuration( ply, sequenceID )
            if duration < 0.25
                return false

            cSound = ply.m_csUnknownTauntSound
            if cSound and cSound\IsPlaying!
                cSound\Stop!

            if isvector( startOrigin )
                ply.m_vUnknownTauntOrigin = ply\GetPos!
                ply\SetPos( startOrigin )

            if isangle( startAngles )
                ply.m_aUnknownTauntAngles = ply\EyeAngles!
                SetNW2Var( ply, "uTaunt-angles", startAngles )
                ply\SetEyeAngles( startAngles )
                ply\SetAngles( startAngles )

            unless utaunt_collisions\GetBool!
                ply.m_iUnknownTauntCollisionGroup = ply\GetCollisionGroup!
                ply.m_bUnknownTauntAvoidPlayers = ply\GetAvoidPlayers!
                ply\SetAvoidPlayers( false )

            unless utaunt_allow_weapon\GetBool!
                weapon = ply\GetActiveWeapon!
                if weapon and IsValid( weapon )
                    ply.m_sUnknownTauntWeapon = weapon\GetClass!
                    ply\SetActiveWeapon!

            unless cycle
                cycle = 0

            SetNW2Var( ply, "uTaunt-start", CurTime! - ( cycle * duration ) )
            SetNW2Var( ply, "uTaunt-name", sequenceName )

            ply\AddVCDSequenceToGestureSlot( GESTURE_SLOT_CUSTOM, sequenceID, 0, true )

            if cycle < 0.025
                soundPath = "unknown-taunts/" .. lower( sequenceName ) .. ".mp3"
                if Exists( "sound/" .. soundPath, "GAME" )
                    cSound = CreateSound( ply, soundPath )
                    ply.m_csUnknownTauntSound = cSound
                    cSound\ChangeVolume( 0 )
                    cSound\SetDSP( 1 )
                    cSound\Play!

                    cSound\ChangeVolume( 1, 1 )

            Run( "PlayerStartTaunt", ply, ACT_GMOD_TAUNT_DANCE, duration )
            Run( "PlayerStartUnknownTaunt", ply, sequenceName, duration )
            return true

    lib.Join = ( ply, otherPlayer ) ->
        sequenceName = GetNW2Var( otherPlayer, "uTaunt-name" )
        if sequenceName == nil
            return false

        sequenceID = LookupSequence( ply, sequenceName )
        if sequenceID < 1
            return false

        unless isnumber( coopDances[ sequenceName ] )
            return lib.Start( ply, sequenceName, true, getCycle( otherPlayer, sequenceID, GetNW2Var( otherPlayer, "uTaunt-start" ) or CurTime! ) )

        danceName, danceID = match( sequenceName, "^([%a_]+_?)(%d+)$" )
        if danceName == nil or danceID == nil
            return false

        danceID = tonumber( danceID )
        if danceID == nil
            return false

        players = otherPlayer.m_tUnknownTauntPlayers
        for index = 1, coopDances[ sequenceName ]
            if not isValidTauntingPlayer( players[ index ] ) or players[ index ] == ply
                players[ index ] = ply
                return lib.Start( ply, danceName .. ( danceID + index ), true, getCycle( otherPlayer, sequenceID, GetNW2Var( otherPlayer, "uTaunt-start" ) or CurTime! ), otherPlayer\GetPos!, otherPlayer\EyeAngles! )

        return false

    do

        COLLISION_GROUP_PASSABLE_DOOR = COLLISION_GROUP_PASSABLE_DOOR
        :GetCollisionGroup = ENTITY
        sequenceID = 0

        Add "PlayerPostThink", addonName, ( ply ) ->
            sequenceName = GetNW2Var( ply, "uTaunt-name" )
            if sequenceName == nil
                return

            unless Alive( ply )
                lib.Stop( ply )
                return

            sequenceID = LookupSequence( ply, sequenceName )
            if sequenceID < 1 or getCycle( ply, sequenceID, getStartTime( ply ) ) == 1
                lib.Stop( ply )
                return

            if utaunt_collisions\GetBool!
                return

            if GetCollisionGroup( ply ) == COLLISION_GROUP_PASSABLE_DOOR
                return

            SetCollisionGroup( ply, COLLISION_GROUP_PASSABLE_DOOR )
            return

    concommand.Add "utaunt", ( ply, _, args ) ->
        unless ply and IsValid( ply ) and Alive( ply )
            return

        if isPlayingTaunt( ply )
            lib.Stop( ply )
            return

        if ply\IsCPlayingTaunt!
            return

        if isstring( args[ 1 ] )
            lib.Start( ply, args[ 1 ], false )
            return

    Add "PlayerShouldTaunt", addonName, =>
        if isPlayingTaunt( @ )
            return false

    allowedActs = {
        [ ACT_GMOD_TAUNT_DANCE ]: true
        [ ACT_GMOD_TAUNT_ROBOT ]: true
        [ ACT_GMOD_TAUNT_CHEER ]: true
        [ ACT_GMOD_TAUNT_LAUGH ]: true
        [ ACT_GMOD_TAUNT_SALUTE ]: true
        [ ACT_GMOD_TAUNT_MUSCLE ]: true
        [ ACT_GMOD_TAUNT_PERSISTENCE ]: true

        [ ACT_GMOD_GESTURE_BOW ]: true
        [ ACT_GMOD_GESTURE_WAVE ]: true
        [ ACT_GMOD_GESTURE_AGREE ]: true
        [ ACT_GMOD_GESTURE_BECON ]: true
        [ ACT_GMOD_GESTURE_DISAGREE ]: true

        [ ACT_GMOD_GESTURE_RANGE_ZOMBIE ]: true
        [ ACT_GMOD_GESTURE_TAUNT_ZOMBIE ]: true
        [ ACT_GMOD_GESTURE_RANGE_ZOMBIE_SPECIAL ]: true

        [ ACT_GMOD_GESTURE_ITEM_GIVE ]: true
        [ ACT_GMOD_GESTURE_ITEM_DROP ]: true
        [ ACT_GMOD_GESTURE_ITEM_PLACE ]: true
        [ ACT_GMOD_GESTURE_ITEM_THROW ]: true

        [ ACT_SIGNAL_FORWARD ]: true
        [ ACT_SIGNAL_GROUP ]: true
        [ ACT_SIGNAL_HALT ]: true
    }

    Add "PlayerShouldUnknownTaunt", "Garry's Mod", ( ply, sequenceID ) ->
        if allowedActs[ ply\GetSequenceActivity( sequenceID ) ]
            return true

Add "PlayerSwitchWeapon", addonName, =>
    if @IsPlayingTaunt!
        return true

do

    :SetRenderAngles = PLAYER

    Add "UpdateAnimation", addonName, ( ply ) ->
        if isPlayingTaunt( ply )
            angles = GetNW2Var( ply, "uTaunt-angles" )
            if angles == nil
                return

            SetRenderAngles( ply, angles )
            return

unless CLIENT
    return

getPhrase = nil
do

    :GetPhrase = language

    getPhrase = ( placeholder ) ->
        fulltext = GetPhrase( placeholder )
        if fulltext == placeholder and sub( placeholder, 1, 15 ) == "unknown_taunts."
            return GetPhrase( sub( placeholder, 16 ) )

        return fulltext

    lib.GetPhrase = getPhrase

do

    mins, maxs = Vector( -512, -512, 0 ), Vector( 512, 512, 512 )

    Add "EntityNetworkedVarChanged", addonName, ( entity, key, _, value ) ->
        unless IsValid( entity ) and entity\IsPlayer! and Alive( entity )
            return

        if key == "uTaunt-name"
            if value == nil
                entity\AnimResetGestureSlot( GESTURE_SLOT_CUSTOM )
                entity\SetRenderBounds( entity\GetModelRenderBounds! )
                return

            sequenceID = LookupSequence( entity, value )
            if sequenceID < 1
                return

            entity\AddVCDSequenceToGestureSlot( GESTURE_SLOT_CUSTOM, sequenceID, getCycle( entity, sequenceID, getStartTime( entity ) ), true )
            mins[ 3 ] = entity\GetModelRenderBounds![ 3 ]
            entity\SetRenderBounds( mins, maxs )
            return

        if key == "uTaunt-start"
            sequenceName = GetNW2Var( entity, "uTaunt-name" )
            if sequenceName == nil
                return

            sequenceID = LookupSequence( entity, sequenceName )
            if sequenceID < 1
                return

            entity\AddVCDSequenceToGestureSlot( GESTURE_SLOT_CUSTOM, sequenceID, getCycle( entity, sequenceID, value ), true )
            mins[ 3 ] = entity\GetModelRenderBounds![ 3 ]
            entity\SetRenderBounds( mins, maxs )
            return

Add "NotifyShouldTransmit", addonName, ( entity, shouldtransmit ) ->
    unless shouldtransmit and IsValid( entity ) and entity\IsPlayer! and Alive( entity )
        return

    sequenceName = GetNW2Var( entity, "uTaunt-name" )
    if sequenceName == nil
        return

    sequenceID = LookupSequence( entity, sequenceName )
    if sequenceID < 1
        return

    entity\AddVCDSequenceToGestureSlot( GESTURE_SLOT_CUSTOM, sequenceID, getCycle( entity, sequenceID, getStartTime( entity ) ), true )
    return

lib.ToggleMenu = ( ply ) ->
    if ply\IsCPlayingTaunt!
        return

    if isPlayingTaunt( ply )
        RunConsoleCommand( "utaunt" )
        return

    panel = lib.Panel
    if panel and panel\IsValid!
        panel\Remove!
        return

    if Run( "AllowUnknownTauntMenu", ply ) == false
        return

    panel = vgui.Create( "uTaunt::Menu" )
    lib.Panel = panel
    panel\Setup( ply )

concommand.Add "utaunts", lib.ToggleMenu

do

    IsFirstTimePredicted = IsFirstTimePredicted
    KEY_F7, KEY_SPACE = KEY_F7, KEY_SPACE

    Add "PlayerButtonDown", addonName, ( ply, keyCode ) ->
        unless IsFirstTimePredicted!
            return

        if keyCode == KEY_F7
            bind = input.LookupKeyBinding( keyCode )
            if bind and #bind ~= 0
                return

            lib.ToggleMenu( ply )
            return

        if keyCode == KEY_SPACE and isPlayingTaunt( ply )
            RunConsoleCommand( "utaunt" )
            return

do

    commmands = { "taunt", "dance", "utaunt", "udance" }
    allowedChars = { "/": true, "!": true }
    LocalPlayer = LocalPlayer

    Add "OnPlayerChat", addonName, ( ply, text, isTeam, isDead ) ->
        if isDead or isTeam or ply ~= LocalPlayer!
            return

        text = lower( Trim( text ) )

        if allowedChars[ sub( text, 1, 1 ) ] == nil
            return

        text = sub( text, 2 )

        for command in *commmands
            if find( text, command, 1, false ) ~= nil
                lib.ToggleMenu( ply )
                return true

-- Garry's Mod
do

    acts = {
        ACT_GMOD_TAUNT_DANCE,
        ACT_GMOD_TAUNT_ROBOT,
        ACT_GMOD_TAUNT_CHEER,
        ACT_GMOD_TAUNT_LAUGH,
        ACT_GMOD_TAUNT_SALUTE,
        ACT_GMOD_TAUNT_MUSCLE,
        ACT_GMOD_TAUNT_PERSISTENCE,
        ACT_GMOD_GESTURE_BOW,
        ACT_GMOD_GESTURE_WAVE,
        ACT_GMOD_GESTURE_AGREE,
        ACT_GMOD_GESTURE_BECON,
        ACT_GMOD_GESTURE_DISAGREE,

        ACT_GMOD_GESTURE_RANGE_ZOMBIE,
        ACT_GMOD_GESTURE_TAUNT_ZOMBIE,
        ACT_GMOD_GESTURE_RANGE_ZOMBIE_SPECIAL,

        ACT_GMOD_GESTURE_ITEM_GIVE,
        ACT_GMOD_GESTURE_ITEM_DROP,
        ACT_GMOD_GESTURE_ITEM_PLACE,
        ACT_GMOD_GESTURE_ITEM_THROW,

        ACT_SIGNAL_FORWARD,
        ACT_SIGNAL_GROUP,
        ACT_SIGNAL_HALT
    }

    Add "UnknownTauntMenuSetup", "Garry's Mod", ( add ) =>
        sequences, sequencesCount = {}, 0
        for act in *acts
            sequenceID = @SelectWeightedSequence( act )
            if sequenceID < 1
                continue

            sequencesCount += 1
            sequences[ sequencesCount ] = @GetSequenceName( sequenceID )

        if sequencesCount == 0
            return

        add( "Garry's Mod:", sequences )
        return

do

    PANEL = {}

    PANEL.Init = =>
        @SetTitle( "#unknown_taunts.menu.title" )
        @SetSize( ScreenScale( 128 ), 24 )
        @SetIcon( "icon16/user.png" )
        @MakePopup!
        @Center!

    PANEL.Setup = ( ply ) =>
        scrollPanel = @Add( "DScrollPanel" )
        @ScrollPanel = scrollPanel
        scrollPanel\Dock( FILL )

        scrollPanel.PerformLayout = ( _, width, height ) ->
            canvas = scrollPanel\GetCanvas!
            if canvas and canvas\IsValid!
                margin = ScreenScale( 2 )
                canvas\DockPadding( margin, 0, margin, margin )

            DScrollPanel.PerformLayout( scrollPanel, width, height )

        Run "UnknownTauntMenuSetup", ply, ( title, sequences ) ->
            if not istable( sequences )
                return

            length = #sequences
            if length == 0
                return

            for index = 1, length
                if Run( "AllowUnknownTaunt", ply, sequences[ index ], title ) == false
                    sequences[ index ] = false
                    length -= 1

            if length == 0
                return

            combo = @[ title ]
            unless combo and combo\IsValid!
                label = scrollPanel\Add( "DLabel" )
                label\SetText( title )
                label\Dock( TOP )

                combo = scrollPanel\Add( "DComboBox" )
                @[ title ] = combo

                combo\SetText( "#unknown_taunts.menu.select" )
                combo\Dock( TOP )

                combo.OnSelect = ( _, __, ___, name ) ->
                    RunConsoleCommand( "utaunt", name )
                    @Close!

            for index = 1, length
                if sequences[ index ] ~= false
                    combo\AddChoice( getPhrase( "unknown_taunts." .. sequences[ index ] ), sequences[ index ] )

    PANEL.PerformLayout = ( width, height ) =>
        scrollPanel = @ScrollPanel
        if scrollPanel and scrollPanel\IsValid!
            height = 0
            for pnl in *scrollPanel\GetCanvas!\GetChildren!
                height += pnl\GetTall!

            if height == 0
                @Close!
                return

            @SetTall( math.min( height + 48, ScrH! * 0.5 ) )

        DFrame.PerformLayout( @, width, height )

    do

        :DrawRect, :SetDrawColor = surface

        PANEL.Paint = ( width, height ) =>
            SetDrawColor( 50, 50, 50, 220 )
            DrawRect( 0, 0, width, height )

    vgui.Register( "uTaunt::Menu", PANEL, "DFrame" )
